plugins {
  id 'application'
  alias(libs.plugins.lombok)
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of("${javaVersion}")
  }
}

dependencies {
  implementation enforcedPlatform(libs.spring.bom)

  implementation 'org.springframework:spring-context'
  implementation 'org.springframework:spring-context-support'

  implementation 'org.springframework:spring-aspects'

  // implementation 'org.springframework:spring-instrument'
  runtimeOnly(libs.aspectjweaver)

  implementation(libs.ehcache)

  testImplementation 'org.springframework:spring-test'
}

apply from: "../../../gradle/default-settings.gradle"


String aspectjweaverPath() {
  return sourceSets.getByName("main").runtimeClasspath.find {
    cls -> return cls.getName().contains("aspectjweaver")
  }
}

final loadTimeWeaverPath = aspectjweaverPath()

test {
  jvmArgs =
      [
          "--add-opens=java.base/java.lang=ALL-UNNAMED",
          "-javaagent:${loadTimeWeaverPath}"
      ]
}

application {
  applicationDefaultJvmArgs =
      [
          "--add-opens=java.base/java.lang=ALL-UNNAMED",
          "-javaagent:${loadTimeWeaverPath}"
      ]
  mainClass = 'org.fp024.study.spring5recipes.caching.App'
}