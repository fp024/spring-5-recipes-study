plugins {
  id 'application'
  alias(libs.plugins.lombok)
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of("${javaVersion}")
  }
}

apply from: "../../gradle/default-settings.gradle"

dependencies {
  implementation enforcedPlatform(libs.spring.bom.v5.snapshot)

  implementation 'org.springframework:spring-context'
  implementation 'org.springframework:spring-aspects'
  implementation 'org.springframework:spring-jdbc'

  runtimeOnly (libs.aspectjweaver)

  runtimeOnly (libs.hsqldb)  

  implementation (libs.javax.annotation.api)
}

String aspectjweaverPath() {
  return sourceSets.getByName("main").runtimeClasspath.find {
    cls -> return cls.getName().contains("aspectjweaver")
  }
}

final ASPECTJ_WEAVER_PATH = aspectjweaverPath()

test {
  jvmArgs = 
    [
      "--add-opens=java.base/java.lang=ALL-UNNAMED",
      "-javaagent:${ASPECTJ_WEAVER_PATH}"
    ]
}

application {
  applicationDefaultJvmArgs = 
    [
      "--add-opens=java.base/java.lang=ALL-UNNAMED",
      "-javaagent:${ASPECTJ_WEAVER_PATH}"
    ]
  mainClass = 'org.fp024.study.spring5recipes.bookshop.Main'
}