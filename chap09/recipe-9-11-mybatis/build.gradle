plugins {
  id "application"
  id "io.freefair.lombok" version "${lombokPluginVersion}"
  id "io.spring.dependency-management" version "${springDependencyManagementPluginVersion}"
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of("${javaVersion}")
  }
}

dependencyManagement {
  imports {
    mavenBom "org.springframework:spring-framework-bom:${springVersion}"
  }
}

apply from: "../../gradle/default-settings.gradle"

dependencies {
  implementation 'org.springframework:spring-context'
  implementation 'org.springframework:spring-jdbc'

  runtimeOnly "org.hsqldb:hsqldb:${hsqldbVersion}"
  runtimeOnly "com.mysql:mysql-connector-j:${mysqlConnectorJVersion}"
  runtimeOnly "com.h2database:h2:${h2dbVersion}"

  implementation "com.zaxxer:HikariCP:${hikariCpVersion}"
  implementation "org.bgee.log4jdbc-log4j2:log4jdbc-log4j2-jdbc4.1:${log4jdbcVersion}"

  implementation "org.mybatis:mybatis:${mybatisVersion}"
  implementation "org.mybatis:mybatis-spring:${mybatisSpringVersion}"

  testImplementation 'org.springframework:spring-test'
}

String getActiveProfiles() {
  final defaultProfile = 'default'
  def activeProfiles = System.getProperty("spring.profiles.active")
  if (!activeProfiles?.trim()) {
    logger.quiet("activeProfiles is null or empty: ${activeProfiles}")
    logger.quiet("It runs with the default profile: ${defaultProfile}")
    return defaultProfile
  } else {
    logger.quiet("activeProfiles: ${activeProfiles}")
    return "${activeProfiles}"
  }
}

final ACTIVE_PROFILES = getActiveProfiles()

tasks.named('test') {
  systemProperty "spring.profiles.active", "${ACTIVE_PROFILES}"
}

application {
  mainClass = 'org.fp024.study.spring5recipes.vehicle.Main'
  applicationDefaultJvmArgs = ["-Dspring.profiles.active=${ACTIVE_PROFILES}"]
}
