plugins {
  id 'application'
  id "io.freefair.lombok" version "${lombokPluginVersion}"    
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of("${javaVersion}")
  }
}

apply from: "../../gradle/default-settings.gradle"

dependencies {
  implementation "org.springframework:spring-context:${springVersion}"

  runtimeOnly "org.springframework:spring-instrument:${springVersion}"

  implementation "javax.annotation:javax.annotation-api:${javaxAnnotationApiVersion}"

  implementation "org.aspectj:aspectjweaver:${aspectjVersion}"

  implementation "com.google.guava:guava:${guavaVersion}"

  testImplementation "org.springframework:spring-test:${springVersion}"
  testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
  testImplementation "org.hamcrest:hamcrest:${hamcrestVersion}"
}

String instrumentLibPath() {
  return sourceSets.getByName("main").runtimeClasspath.find {
    cls -> return cls.getName().contains("spring-instrument")
  }
}

final INSTRUMENT_LIB_PATH = instrumentLibPath()

test {
  jvmArgs = 
    [
      "--add-opens=java.base/java.lang=ALL-UNNAMED",
      "-javaagent:${INSTRUMENT_LIB_PATH}"
    ]
}

application {
  // https://stackoverflow.com/questions/68117860/eclipse-2021-06-classformaterror-accessible-module-java-base-does-not-opens-j
  applicationDefaultJvmArgs = 
	[
      "--add-opens=java.base/java.lang=ALL-UNNAMED",
      "-javaagent:${INSTRUMENT_LIB_PATH}"
	] 
  mainClass = 'org.fp024.study.spring5recipes.calculator.Main'
}