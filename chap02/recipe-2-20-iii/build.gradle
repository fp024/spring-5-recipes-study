plugins {
  id 'application'
  alias(libs.plugins.lombok)
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of("${javaVersion}")
  }
}

apply from: "../../gradle/default-settings.gradle"

dependencies {
  implementation enforcedPlatform(libs.spring.bom.v5)

  implementation "org.springframework:spring-context"

  runtimeOnly "org.springframework:spring-instrument"

  implementation (libs.javax.annotation.api)
  implementation (libs.aspectjweaver)
}

String instrumentLibPath() {
  return sourceSets.getByName("main").runtimeClasspath.find {
    cls -> return cls.getName().contains("spring-instrument")
  }
}

final INSTRUMENT_LIB_PATH = instrumentLibPath()

test {
  jvmArgs = 
    [
      "--add-opens=java.base/java.lang=ALL-UNNAMED",
      "-javaagent:${INSTRUMENT_LIB_PATH}"
    ]
}

application {
  // https://stackoverflow.com/questions/68117860/eclipse-2021-06-classformaterror-accessible-module-java-base-does-not-opens-j
  applicationDefaultJvmArgs = 
	[
      "--add-opens=java.base/java.lang=ALL-UNNAMED",
      "-javaagent:${INSTRUMENT_LIB_PATH}"
	] 
  mainClass = 'org.fp024.study.spring5recipes.calculator.Main'
}