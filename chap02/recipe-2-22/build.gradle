plugins {
  id 'application'
  alias(libs.plugins.lombok)
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of("${javaVersion}")
  }
}

apply from: "../../gradle/default-settings.gradle"

dependencies {
  implementation enforcedPlatform(libs.spring.bom.v5)

  implementation "org.springframework:spring-context"
  implementation "org.springframework:spring-aspects"
  
  // aspect 컴파일러 경고 때문에 추가. 예제에서 트랜젝션이나 DB를 사용하진 않음.
  implementation "org.springframework:spring-tx"

  implementation (libs.javax.annotation.api)
  implementation (libs.aspectjweaver)
}


String instrumentLibPath() {
  return sourceSets.getByName("main").compileClasspath.find {
    cls -> return cls.getName().contains("aspectjweaver")
  }
}

final INSTRUMENT_LIB_PATH = instrumentLibPath()

test {
  jvmArgs =
    [
      "--add-opens=java.base/java.lang=ALL-UNNAMED",
      "-javaagent:${INSTRUMENT_LIB_PATH}"
    ]
}

application {
  applicationDefaultJvmArgs = 
    [
      "--add-opens=java.base/java.lang=ALL-UNNAMED",
      "-javaagent:${INSTRUMENT_LIB_PATH}"
    ]
  mainClass = 'org.fp024.study.spring5recipes.calculator.Main'
}