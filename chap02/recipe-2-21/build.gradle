plugins {
  id 'application'
  id "io.freefair.lombok" version "${lombokPluginVersion}"    
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of("${javaVersion}")
  }
}

apply from: "../../gradle/default-settings.gradle"

dependencies {
  implementation "org.springframework:spring-context:${springVersion}"

  implementation "javax.annotation:javax.annotation-api:${javaxAnnotationApiVersion}"

  implementation "org.aspectj:aspectjweaver:${aspectjVersion}"    

  testImplementation "org.springframework:spring-test:${springVersion}"
  testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
}

String instrumentLibPath() {
  return sourceSets.getByName("main").compileClasspath.find {
    cls -> return cls.getName().contains("aspectjweaver")
  }
}

final INSTRUMENT_LIB_PATH = instrumentLibPath()

test {
  jvmArgs =
    [
      "--add-opens=java.base/java.lang=ALL-UNNAMED",
      "-javaagent:${INSTRUMENT_LIB_PATH}"
    ]
}

application {
  applicationDefaultJvmArgs = 
    [
      "--add-opens=java.base/java.lang=ALL-UNNAMED",
      "-javaagent:${INSTRUMENT_LIB_PATH}"
    ]
  mainClass = 'org.fp024.study.spring5recipes.calculator.Main'
}